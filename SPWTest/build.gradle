buildscript {
	// Dependency Versions
	ext{
		gaeVer = '1.9.19'
		gwtVer = '2.7.0'
		gspVer = '0.6.1'
		groupId = 'com.gdevelop.gwt.syncrpc'
	}
	
	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies {
		classpath "com.google.appengine:gradle-appengine-plugin:$gaeVer"
		classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
	}
}

repositories{
	mavenCentral()
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'appengine'
apply plugin: 'gwt'

classes.dependsOn 'gwttestservicesClasses'

sourceSets {
	gwttestservices{
		java{
			srcDir "../google-web-toolkit/user/test"
			include '**/safehtml/shared/GwtSafeHtmlBuilderTest.java'
			include '**/safehtml/shared/GwtSafeHtmlStringTest.java'
			include '**/client/rpc/*.java'
			include 'com/google/gwt/user/RPCSuite.gwt.xml'
			include '**/server/rpc/*ServerCustomFieldSerializer.java'
			include '**/server/rpc/*ServiceImpl.java'
			include '**/server/rpc/*TestServiceImpl*.java'
			include '**/server/rpc/LogFilterServletContext.java'
			include '**/server/rpc/RpcTokenAwareRemoteService.java'
			
			include '**/i18n/client/CustomPluralsTest.java'
			include '**/junit/client/GWTTestCaseStackTraceTest.java'
			include '**/junit/client/GWTTestCaseTest*.java'
			include '**/resources/client/CSSResourceTest.java'
			include '**/validation/client/ValidationClientGwtTestCase.java'
			include '**/validation/client/constraints/PatternValidatorGwtTest.java'
			include '**/junit/client/*ExceptionAsserter.java'
			include '**/junit/client/ExpectedFailure.java'
			include '**/resources/client/Custom*Resource.java'
			include '**/resources/client/NestedBundleTest.java'
			include '**/resources/client/gss/ImportResource.java'
			include '**/resources/client/impl/*.java'
			include '**/resources/rg/Custom*ResourceGenerator.java'
		}
	}
	main{
		java{
			srcDir 'src/main/java'
			include '**/com/gdevelop/**'
		}
		compileClasspath += gwttestservices.output
	}
}

task gwttestservicesJar(type: Jar){
	baseName = 'GWTTestServices'
	from sourceSets.gwttestservices.output
}

sourceCompatibility = 1.7

war.dependsOn gwttestservicesJar

dependencies {
	gwttestservicesCompile "com.google.gwt:gwt-user:$gwtVer"
	gwttestservicesCompile "com.google.gwt:gwt-dev:$gwtVer"
	gwttestservicesCompile 'junit:junit:4.11'

	compile "com.google.gwt:gwt-user:$gwtVer"
	compile "com.google.gwt:gwt-dev:$gwtVer"
	compile files('build/libs/GWTTestServices.jar')
	compile 'com.google.api-client:google-api-client-servlet:1.20.0'
	compile 'com.google.api-client:google-api-client-gson:1.20.0'
	compile "com.google.appengine:appengine-api-1.0-sdk:$gaeVer"
	appengineSdk "com.google.appengine:appengine-java-sdk:$gaeVer"

	compile 'junit:junit:4.11'
	testCompile "com.google.appengine:appengine-testing:$gaeVer"
	testCompile "com.google.appengine:appengine-api-labs:$gaeVer"
	testCompile "com.google.appengine:appengine-api-stubs:$gaeVer"
}

gwt{
	//gwtVersion='2.7.0' // Including this seems to remove -user and -dev, along with several app engine dependencies from the gradle listing in Eclipse
	modules 'com.gdevelop.gwt.syncrpc.spawebtest.SPAWebTest'
	devModules 'com.gdevelop.gwt.syncrpc.spawebtest.SPAWebTest'
	// logLevel = 'INFO'
	src += files(sourceSets.gwttestservices.allJava.srcDirs)
	eclipse {
		addGwtContainer=false
	}
}
configurations{
	gwttestservices
}
appengine {
	downloadSdk = true
	jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']
	httpPort = 8888
	httpAddress = '192.168.1.107'
	appcfg{
		oauth2 = true
	}
}

war{
	classpath configurations.gwttestservices
}

apply from: 'publishing.gradle'